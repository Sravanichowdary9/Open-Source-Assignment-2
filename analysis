#!/bin/bash

# Usage: ./analysis cleaned.tsv
# This script answers 4 questions using the cleaned TSV file:
# 1. Most popular game mechanics
# 2. Most popular game domain
# 3. Correlation between year and rating
# 4. Correlation between complexity and rating


file="bgg_dataset.tsv"


##############################################
# 1. Most Popular Game Mechanics
# Lecture 14–15 (awk3) — associative arrays and split()
##############################################
awk -F'\t' '
NR > 1 && $13 != "" {
    n = split($13, mech, /, */)
    for (i = 1; i <= n; i++) {
        count[mech[i]]++
    }
}
END {
    max = 0
    for (m in count) {
        if (count[m] > max) {
            max = count[m]
            top = m
        }
    }
    print "The most popular game mechanics is " top " found in " max " games"
}' "$file"

##############################################
# 2. Most Popular Game Domain
# Lecture 15 — repeated logic with domain column ($14)
##############################################
awk -F'\t' '
NR > 1 && $14 != "" {
    n = split($14, dom, /, */)
    for (i = 1; i <= n; i++) {
        count[dom[i]]++
    }
}
END {
    max = 0
    for (d in count) {
        if (count[d] > max) {
            max = count[d]
            top = d
        }
    }
    print "The most game domain is " top " found in " max " games"
}' "$file"

##############################################
# 3. Correlation: Year Published vs Rating
# Based on assignment formula + Lecture 13–15 awk arithmetic
##############################################
awk -F'\t' '
NR > 1 && $3 ~ /^[0-9]+$/ && $9 ~ /^[0-9.]+$/ {
    x = $3 + 0
    y = $9 + 0
    sum_x += x
    sum_y += y
    sum_x2 += x*x
    sum_y2 += y*y
    sum_xy += x*y
    n++
}
END {
    num = n * sum_xy - sum_x * sum_y
    den = sqrt((n * sum_x2 - sum_x^2) * (n * sum_y2 - sum_y^2))
    corr = (den != 0) ? num / den : 0
    printf "The correlation between the year of publication and the average rating is %.3f\n", corr
}' "$file"

##############################################
# 4. Correlation: Complexity vs Rating
# Same logic, using $11 (Complexity) and $9 (Rating)
##############################################
awk -F'\t' '
NR > 1 && $11 ~ /^[0-9.]+$/ && $9 ~ /^[0-9.]+$/ {
    x = $11 + 0
    y = $9 + 0
    sum_x += x
    sum_y += y
    sum_x2 += x*x
    sum_y2 += y*y
    sum_xy += x*y
    n++
}
END {
    num = n * sum_xy - sum_x * sum_y
    den = sqrt((n * sum_x2 - sum_x^2) * (n * sum_y2 - sum_y^2))
    corr = (den != 0) ? num / den : 0
    printf "The correlation between the complexity of a game and its average rating is %.3f\n", corr
}' "$file"

